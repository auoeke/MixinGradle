plugins {
    id("groovy-gradle-plugin")
    id("com.github.hierynomus.license").version("latest.integration")
}

plugins.apply("maven-publish")

defaultTasks 'licenseFormat', 'build'

group = 'org.spongepowered'
version = "0.8-SNAPSHOT"

compileGroovy {
    targetCompatibility = 16
    sourceCompatibility = 16
}

repositories {
    mavenCentral()

    maven {
        url 'https://repo.gradle.org/gradle/libs-releases-local/'
    }
}

dependencies {
    implementation gradleApi()
    implementation 'com.google.guava:guava:21.0'
    implementation 'org.ow2.asm:asm-tree:latest.integration'
    //deployerJars 'org.apache.maven.wagon:wagon-ftp:2.7'
}

processResources {
   from 'LICENSE.txt'
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }

    include '**/*.groovy'
    header file('HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

jar {
    manifest.mainAttributes (
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": buildVersion
    )
}

java {
    withSourcesJar()
}

gradlePlugin {
    plugins {
        patcher {
            id = "mixin"
            implementationClass = "org.spongepowered.asm.gradle.plugins.MixinGradlePlugin"
        }
    }
}

publishing {
    repositories {
        maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
    }
}
